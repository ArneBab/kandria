(in-package #:org.shirakumo.fraf.kandria)

(defclass module-v0 (version) ())
(defclass module-v1 (version) ())

(defmethod supported-p ((_ module-v0)) T)

(defvar *module-class-name*)

(define-decoder (module module-v0) (initargs depot)
  (let ((*module-class-name* NIL))
    (load-source-file (depot:entry "setup.lisp" depot))
    (let* ((id (getf initargs :id))
           (class (find-class *module-class-name* NIL))
           (module (find-module id)))
      (when (or (null *module-class-name*) (null class) (not (c2mop:subclassp class 'module)))
        (error "Mod ~a does not define a module class!" depot))
      (load-module (apply #'ensure-instance module class initargs)))))

(define-encoder (module module-v0) (_b depot)
  (depot:with-open (tx (depot:ensure-entry "meta.lisp" depot) :output 'character)
    (let ((*standard-output* (depot:to-stream tx)))
      (princ* `(:identifier module :version ,(type-of module-v0)))
      (princ* `(:id ,(id module)
                :title ,(title module)
                :author ,(author module)
                :version ,(version module)
                :description ,(description module)
                :upstream ,(upstream module)))))
  (unless (depot:entry-exists-p "setup.lisp" depot)
    (depot:with-open (tx (depot:ensure-entry "setup.lisp" depot) :output 'character)
      (let ((*standard-output* (depot:to-stream tx)))
        (princ* `(define-module ,(id module)))
        (terpri)
        (princ* `(in-package ,(format NIL "~a.~a" '#:org.shirakumo.fraf.kandria.mod (id module))))
        (terpri)
        (princ* `(defmethod load-module ((module module))))))))
