(in-package #:org.shirakumo.fraf.kandria)

(defclass race-results-screen (pausing-panel menuing-panel)
  ())

(defmethod initialize-instance :after ((panel race-results-screen) &key (storyline (storyline +world+)))
  (let* ((layout (make-instance 'big-prompt-layout))
         (focus (make-instance 'alloy:focus-list))
         (title (make-instance 'header :level 0 :value (@ stats-screen-title)))
         (clipper (make-instance 'alloy:clip-view :limit :x))
         (scroll (alloy:represent-with 'alloy:y-scrollbar clipper))
         (grid (make-instance 'alloy:grid-layout :col-sizes '(T 250) :row-sizes '(30) :cell-margins (alloy:margins 10 -5))))
    (alloy:enter title layout :constraints `((:fill :w) (:top 50) (:height 50)))
    (alloy:enter grid clipper)
    (flet ((format-relative-time (clock)
             (multiple-value-bind (ts ms) (floor clock)
               (multiple-value-bind (tm s) (floor ts 60)
                 (format NIL "~2,'0d:~2,'0d.~2,'0d"
                         tm s (floor (* 100 ms)))))))
      (loop for name in (reverse *race-quests*)
            for quest = (quest:find-quest name storyline)
            do (when (quest:var 'pb quest)
                 (make-instance 'label :value (quest:title quest) :layout-parent grid)
                 (make-instance 'label :value (format-relative-time (quest:var 'pb quest)) :layout-parent grid :style `((:label :halign :right))))))
    (let ((return (alloy:represent (@ continue-to-next) 'button :focus-parent focus)))
      (alloy:enter return layout :constraints `((:bottom 40) (:size 300 40) (:center :w)))
      (alloy:enter clipper layout :constraints `((:center :w) (:below ,title 20) (:weak (:above ,return 20)) (:width 600)))
      (alloy:enter scroll layout :constraints `((:chain :right ,clipper)))
      (alloy:on alloy:activate (return)
        (hide panel))
      (alloy:on alloy:exit (focus)
        (setf (alloy:focus focus) :strong)
        (setf (alloy:focus return) :weak)))
    (alloy:finish-structure panel layout focus)))
